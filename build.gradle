plugins {
    id "net.ltgt.apt" version "0.19"
    id "com.jfrog.bintray" version "1.8.4"
}

allprojects {
    apply plugin: "net.ltgt.apt"
    apply plugin: "net.ltgt.apt-idea"
    apply plugin: "net.ltgt.apt-eclipse"

    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    if (project.name != "docs") {
        archivesBaseName = "${rootProject.name}-${project.name.replace(':', '-')}"
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
        options.encoding = "UTF-8"
    }

    sourceCompatibility = targetCompatibility = 1.8

    dependencies {
        if (project.name != "bom" && project.name != "docs") {
            compile "org.slf4j:slf4j-api:1.7.25"
            
            testCompile "ch.qos.logback:logback-classic:1.2.3"
        }
    }

    apply from: "${rootDir}/gradle/deploy.gradle"

    test {
        useJUnitPlatform()
    }
}


rootProject.bintrayUpload.enabled = false
rootProject.bintrayPublish.enabled = false

wrapper {
    gradleVersion = "5.0"
    distributionType = Wrapper.DistributionType.ALL
}